// Загружает курсы валют на текущую дату.
//
//
Процедура ЗагрузитьАктуальныеКурсыЦБ() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ЗагрузкаКурсовВалютЦБ);
	
	ИмяСобытия = НСтр("ru = 'Валюты.Загрузка курсов валют'",
		ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Начата регламентная загрузка курсов валют с сайта ЦБ'"));
	
	ТекДата = ТекущаяДатаСеанса();
	
	РезультатЗагрузки=Обработки.ЗагрузкаКурсовВалютЦБ.Создать().ПолучитьКурсыВалют(ТекДата);
	
	Если РезультатЗагрузки=0 Тогда
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			, 
			,
			НСтр("ru = 'Во время регламентного задания загрузки курсов валют ЦБ возникла ошибка подключения'"));
		ВызватьИсключение НСтр("ru = 'Загрузка курсов не выполнена. Подробности см. в журнале регистрации.'");
	ИначеЕсли РезультатЗагрузки<0 Тогда
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			, 
			,
			НСтр("ru = 'Во время регламентного задания загрузки курсов валют ЦБ данные для используемых валют не обнаружены'"));
		ВызватьИсключение НСтр("ru = 'Загрузка курсов не выполнена. Подробности см. в журнале регистрации.'");
	Иначе
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Информация,
			,
			,
			НСтр("ru = 'Завершена регламентная загрузка курсов валют.'"));
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

//Добавить рубль
Функция ПолучитьРубль() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Результат=Справочники.Валюты.НайтиПоКоду("643");
	Если Результат.Пустая() тогда
		КлассификаторXML = Справочники.Валюты.ПолучитьМакет("ОбщероссийскийКлассификаторВалют").ПолучитьТекст();
		
		КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
		ЗаписьОКВ=КлассификаторТаблица.Найти("643","Code");
		Если НЕ ЗаписьОКВ = Неопределено тогда
			НоваяСтрока = Справочники.Валюты.СоздатьЭлемент();
			НоваяСтрока.Код							= ЗаписьОКВ.Code;
			НоваяСтрока.Наименование				= ЗаписьОКВ.CodeSymbol;
			НоваяСтрока.НаименованиеПолное			= ЗаписьОКВ.Name;
			Если ЗаписьОКВ.RBCLoading Тогда
				НоваяСтрока.СпособУстановкиКурса 	= Перечисления.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета;
			Иначе
				НоваяСтрока.СпособУстановкиКурса 	= Перечисления.СпособыУстановкиКурсаВалюты.РучнойВвод;
			КонецЕсли;
			НоваяСтрока.ПараметрыПрописиНаРусском 	= ЗаписьОКВ.NumerationItemOptions;
			НоваяСтрока.Записать();
			Результат=НоваяСтрока.Ссылка;
		КонецЕсли;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Результат;
КонецФункции
